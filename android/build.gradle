def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

plugins {
    id "maven-publish"
    id "com.android.library"
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 30)
    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 16)
        targetSdkVersion safeExtGet('targetSdkVersion', 30)
        versionCode 1
        versionName "1.0"
    }
    sourceSets {
        main {
            java {
                if (safeExtGet('excludeAppGlideModule', false)) {
                    srcDir "src"
                    exclude "**/FastImageGlideModule.java"
                }
            }
        }
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    google()
    mavenCentral()
    if (project == rootProject) {
        maven {
            url "https://a8c-libs.s3.amazonaws.com/android/react-native-mirror"
        }
    } else {
        // When building as a dep, the RN's maven repo is locally in the node_modules folder
        def nodeModulesPath = "${project.buildDir}/../../node_modules/"
        maven { url "${nodeModulesPath}/react-native/android" }
    }
}

def glideVersion = safeExtGet('glideVersion', '4.12.0')

dependencies {
    //noinspection GradleDynamicVersion
    if (project == rootProject) {
        implementation "com.facebook.react:react-native:${safeExtGet('reactnativeVersion', '+')}"
        // If this is the root project (e.g. Jitpack), specify a version
        implementation 'com.facebook.react:react-native:0.66.2'
    } else {
        //noinspection GradleDynamicVersion
        implementation "com.facebook.react:react-native:${safeExtGet('reactnativeVersion', '+')}"
    }
    implementation "com.github.bumptech.glide:glide:${glideVersion}"
    implementation "com.github.bumptech.glide:okhttp3-integration:${glideVersion}"
    annotationProcessor "androidx.annotation:annotation:1.1.0" // from https://github.com/DylanVann/react-native-fast-image/issues/499#issuecomment-541251477
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.wordpress-mobile'
                artifactId = 'react-native-fast-image'
            }
        }
    }
}
